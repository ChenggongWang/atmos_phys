
module zonal_ozone_mod

!=======================================================================

use  time_manager_mod, only: time_type
use   time_interp_mod, only: fraction_of_year
use     utilities_mod, only: error_mesg, FATAL, open_file, &
                             get_my_pe, close_file

implicit none
private

public  zonal_ozone, zonal_ozone_init
!=======================================================================

integer :: iseason = -1
real    :: ph3(82),p3(81),o3hi(10,25)
real    :: o3lo1(10,16),o3lo2(10,16),o3lo3(10,16),o3lo4(10,16)
real    :: ph(82),o3data(19,81,4)

real :: rstd(19,81),current_fyear

real :: pref = 101325.
real :: twopi,rad2deg

character(len=128) :: version = '$Id: zonal_ozone.F90,v 1.2 2000/08/04 19:04:56 fms Exp $'
character(len=128) :: tag = '$Name: bombay $'

!-----------------------------------------------------------------------

interface zonal_ozone
    module procedure geto3_2d, geto3_3d
end interface

!-----------------------------------------------------------------------
!---------------- private data use by this module ----------------------
      data ph3 / 0.,  &
         0.1027246e-04, 0.1239831e-04, 0.1491845e-04, 0.1788053e-04, &
         0.2135032e-04, 0.2540162e-04, 0.3011718e-04, 0.3558949e-04, &
         0.4192172e-04, 0.4922875e-04, 0.5763817e-04, 0.6729146e-04, &
         0.7834518e-04, 0.9097232e-04, 0.1053635e-03, 0.1217288e-03, &
         0.1402989e-03, 0.1613270e-03, 0.1850904e-03, 0.2119495e-03, &
         0.2423836e-03, 0.2768980e-03, 0.3160017e-03, 0.3602623e-03, &
         0.4103126e-03, 0.4668569e-03, 0.5306792e-03, 0.6026516e-03, &
         0.6839018e-03, 0.7759249e-03, 0.8803303e-03, 0.9987843e-03, &
         0.1133178e-02, 0.1285955e-02, 0.1460360e-02, 0.1660001e-02, &
         0.1888764e-02, 0.2151165e-02, 0.2452466e-02, 0.2798806e-02, &
         0.3197345e-02, 0.3656456e-02, 0.4185934e-02, 0.4797257e-02, &
         0.5503893e-02, 0.6321654e-02, 0.7269144e-02, 0.8368272e-02, &
         0.9644873e-02, 0.1112946e-01, 0.1285810e-01, 0.1487354e-01, &
         0.1722643e-01, 0.1997696e-01, 0.2319670e-01, 0.2697093e-01, &
         0.3140135e-01, 0.3660952e-01, 0.4274090e-01, 0.4996992e-01, &
         0.5848471e-01, 0.6847525e-01, 0.8017242e-01, 0.9386772e-01, &
         0.1099026e+00, 0.1286765e+00, 0.1506574e+00, 0.1763932e+00, &
         0.2065253e+00, 0.2415209e+00, 0.2814823e+00, 0.3266369e+00, &
         0.3774861e+00, 0.4345638e+00, 0.4984375e+00, 0.5697097e+00, &
         0.6490189e+00, 0.7370409e+00, 0.8344896e+00, 0.9421190e+00, &
         0.1000000e+01 /
!-----------------------------------------------------------------------
      data p3  &
       / 0.9300000e-05, 0.1129521e-04, 0.1360915e-04, 0.1635370e-04, &
         0.1954990e-04, 0.2331653e-04, 0.2767314e-04, 0.3277707e-04, &
         0.3864321e-04, 0.4547839e-04, 0.5328839e-04, 0.6234301e-04, &
         0.7263268e-04, 0.8450696e-04, 0.9793231e-04, 0.1133587e-03, &
         0.1307170e-03, 0.1505832e-03, 0.1728373e-03, 0.1982122e-03, &
         0.2266389e-03, 0.2592220e-03, 0.2957792e-03, 0.3376068e-03, &
         0.3844381e-03, 0.4379281e-03, 0.4976965e-03, 0.5658476e-03, &
         0.6418494e-03, 0.7287094e-03, 0.8261995e-03, 0.9380076e-03, &
         0.1063498e-02, 0.1207423e-02, 0.1369594e-02, 0.1557141e-02, &
         0.1769657e-02, 0.2015887e-02, 0.2295520e-02, 0.2620143e-02, &
         0.2989651e-02, 0.3419469e-02, 0.3909867e-02, 0.4481491e-02, &
         0.5135272e-02, 0.5898971e-02, 0.6774619e-02, 0.7799763e-02, &
         0.8978218e-02, 0.1036103e-01, 0.1195488e-01, 0.1382957e-01, &
         0.1599631e-01, 0.1855114e-01, 0.2151235e-01, 0.2501293e-01, &
         0.2908220e-01, 0.3390544e-01, 0.3952926e-01, 0.4621349e-01, &
         0.5403168e-01, 0.6330472e-01, 0.7406807e-01, 0.8677983e-01, &
         0.1015345e+00, 0.1189603e+00, 0.1391863e+00, 0.1630739e+00, &
         0.1908004e+00, 0.2235461e+00, 0.2609410e+00, 0.3036404e+00, &
         0.3513750e+00, 0.4055375e+00, 0.4656677e+00, 0.5335132e+00, &
         0.6083618e+00, 0.6923932e+00, 0.7845676e+00, 0.8875882e+00, &
         0.1000000e+01 /
!-----------------------------------------------------------------------
      data o3hi(1:10,1:16) /  &
      0.55, 0.50, 0.45, 0.45, 0.40, 0.35, 0.35, 0.30, 0.30, 0.30, &
      0.55, 0.51, 0.46, 0.47, 0.42, 0.38, 0.37, 0.36, 0.35, 0.35, &
      0.55, 0.53, 0.48, 0.49, 0.44, 0.42, 0.41, 0.40, 0.38, 0.38, &
      0.60, 0.55, 0.52, 0.52, 0.50, 0.47, 0.46, 0.44, 0.42, 0.41, &
      0.65, 0.60, 0.55, 0.56, 0.53, 0.52, 0.50, 0.48, 0.45, 0.45, &
      0.75, 0.65, 0.60, 0.60, 0.55, 0.55, 0.55, 0.50, 0.48, 0.47, &
      0.80, 0.75, 0.75, 0.75, 0.70, 0.70, 0.65, 0.63, 0.60, 0.60, &
      0.90, 0.85, 0.85, 0.80, 0.80, 0.75, 0.75, 0.74, 0.72, 0.71, &
      1.10, 1.05, 1.00, 0.90, 0.90, 0.90, 0.85, 0.83, 0.80, 0.80, &
      1.40, 1.30, 1.25, 1.25, 1.25, 1.20, 1.15, 1.10, 1.05, 1.00, &
      1.70, 1.70, 1.60, 1.60, 1.60, 1.60, 1.60, 1.60, 1.50, 1.50, &
      2.10, 2.00, 1.90, 1.90, 1.90, 1.80, 1.80, 1.80, 1.70, 1.70, &
      2.40, 2.30, 2.20, 2.20, 2.20, 2.10, 2.10, 2.10, 2.00, 2.00, &
      2.70, 2.50, 2.50, 2.50, 2.50, 2.50, 2.40, 2.40, 2.30, 2.30, &
      2.90, 2.80, 2.70, 2.70, 2.70, 2.70, 2.70, 2.70, 2.60, 2.60, &
      3.10, 3.10, 3.00, 3.00, 3.00, 3.00, 3.00, 3.00, 2.90, 2.80 /

      data o3hi(1:10,17:25) /  &
      3.30, 3.40, 3.40, 3.60, 3.70, 3.90, 4.00, 4.10, 4.00, 3.80, &
      3.60, 3.80, 3.90, 4.20, 4.70, 5.30, 5.60, 5.70, 5.50, 5.20, &
      4.10, 4.30, 4.70, 5.20, 6.00, 6.70, 7.00, 6.80, 6.40, 6.20, &
      5.40, 5.70, 6.00, 6.60, 7.30, 8.00, 8.40, 7.70, 7.10, 6.70, &
      6.70, 6.80, 7.00, 7.60, 8.30, 10.0, 9.60, 8.20, 7.50, 7.20, &
      9.20, 9.30, 9.40, 9.60, 10.3, 10.6, 10.0, 8.50, 7.70, 7.30, &
      12.6, 12.1, 12.0, 12.1, 11.7, 11.0, 10.0, 8.60, 7.80, 7.40, &
      14.2, 13.5, 13.1, 12.8, 11.9, 10.9, 9.80, 8.50, 7.80, 7.50, &
      14.3, 14.0, 13.4, 12.7, 11.6, 10.6, 9.30, 8.40, 7.60, 7.30 /
 
      data o3lo1 /  &
      14.9, 14.2, 13.3, 12.5, 11.2, 10.3, 9.50, 8.60, 7.50, 7.40, &
      14.5, 14.1, 13.0, 11.8, 10.5, 9.80, 9.20, 7.90, 7.40, 7.40, &
      11.8, 11.5, 10.9, 10.5, 9.90, 9.60, 8.90, 7.50, 7.20, 7.20, &
      7.30, 7.70, 7.80, 8.40, 8.40, 8.50, 7.90, 7.40, 7.10, 7.10, &
      4.10, 4.40, 5.30, 6.60, 6.90, 7.50, 7.40, 7.20, 7.00, 6.90, &
      1.80, 1.90, 2.50, 3.30, 4.50, 5.80, 6.30, 6.30, 6.40, 6.10, &
      0.40, 0.50, 0.80, 1.20, 2.70, 3.60, 4.60, 4.70, 5.00, 5.20, &
      0.10, 0.15, 0.20, 0.50, 1.40, 2.10, 3.00, 3.20, 3.50, 3.90, &
      0.07, 0.10, 0.12, 0.30, 1.00, 1.40, 1.80, 1.90, 2.30, 2.50, &
      0.06, 0.08, 0.10, 0.15, 0.60, 0.80, 1.40, 1.50, 1.50, 1.60, &
      0.05, 0.05, 0.06, 0.09, 0.20, 0.40, 0.70, 0.80, 0.90, 0.90, &
      0.05, 0.05, 0.06, 0.08, 0.10, 0.13, 0.20, 0.25, 0.30, 0.40, &
      0.05, 0.05, 0.05, 0.06, 0.07, 0.07, 0.08, 0.09, 0.10, 0.13, &
      0.05, 0.05, 0.05, 0.05, 0.06, 0.06, 0.06, 0.06, 0.07, 0.07, &
      0.05, 0.05, 0.05, 0.05, 0.05, 0.05, 0.05, 0.06, 0.06, 0.06, &
      0.04, 0.04, 0.04, 0.04, 0.04, 0.04, 0.04, 0.05, 0.05, 0.05 /

      data o3lo2 /  &
      14.8, 14.2, 13.8, 12.2, 11.0, 9.80, 8.50, 7.80, 7.40, 6.90, &
      13.2, 13.0, 12.5, 11.3, 10.4, 9.00, 7.80, 7.50, 7.00, 6.60, &
      10.6, 10.6, 10.7, 10.1, 9.40, 8.60, 7.50, 7.00, 6.50, 6.10, &
      7.00, 7.30, 7.50, 7.50, 7.50, 7.30, 6.70, 6.40, 6.00, 5.80, &
      3.80, 4.00, 4.70, 5.00, 5.20, 5.90, 5.80, 5.60, 5.50, 5.50, &
      1.40, 1.60, 2.40, 3.00, 3.70, 4.10, 4.60, 4.80, 5.10, 5.00, &
      0.40, 0.50, 0.90, 1.20, 2.00, 2.70, 3.20, 3.60, 4.30, 4.10, &
      0.07, 0.10, 0.20, 0.30, 0.80, 1.40, 2.10, 2.40, 2.70, 3.00, &
      0.06, 0.07, 0.09, 0.15, 0.30, 0.70, 1.20, 1.40, 1.60, 2.00, &
      0.05, 0.05, 0.06, 0.12, 0.15, 0.30, 0.60, 0.70, 0.80, 0.80, &
      0.04, 0.05, 0.06, 0.08, 0.09, 0.15, 0.30, 0.40, 0.40, 0.40, &
      0.04, 0.04, 0.05, .055, 0.06, 0.09, 0.12, 0.13, 0.15, 0.15, &
      0.03, 0.03, .045, .052, .055, 0.06, 0.07, 0.07, 0.06, 0.07, &
      0.03, 0.03, 0.04, .051, .052, .052, 0.06, 0.06, 0.05, 0.05, &
      0.02, 0.02, 0.03, 0.05, 0.05, 0.05, 0.04, 0.04, 0.04, 0.04, &
      0.02, 0.02, 0.02, 0.04, 0.04, 0.04, 0.03, 0.03, 0.03, 0.03 /

      data o3lo3 /  &
      14.5, 14.0, 13.5, 11.3, 11.0, 10.0, 9.00, 8.30, 7.50, 7.30, &
      13.5, 13.2, 12.5, 11.1, 10.4, 9.70, 8.20, 7.80, 7.40, 6.80, &
      10.8, 10.9, 11.0, 10.4, 10.0, 9.60, 7.90, 7.50, 7.00, 6.70, &
      7.30, 7.50, 7.80, 8.50, 9.00, 8.50, 7.70, 7.40, 6.90, 6.50, &
      4.10, 4.50, 5.30, 6.20, 7.30, 7.70, 7.30, 7.00, 6.60, 6.40, &
      1.80, 2.00, 2.20, 3.80, 4.30, 5.60, 6.20, 6.20, 6.40, 6.20, &
      0.30, 0.50, 0.60, 1.50, 2.80, 3.70, 4.50, 4.70, 5.50, 5.60, &
      0.09, 0.10, 0.15, 0.60, 1.20, 2.10, 3.00, 3.50, 4.00, 4.30, &
      0.06, 0.08, 0.10, 0.30, 0.60, 1.10, 1.90, 2.20, 2.90, 3.00, &
      0.04, 0.05, 0.06, 0.15, 0.45, 0.60, 1.10, 1.30, 1.60, 1.80, &
      0.04, 0.04, 0.04, 0.08, 0.20, 0.30, 0.55, 0.60, 0.75, 0.90, &
      0.04, 0.04, 0.04, 0.05, 0.06, 0.10, 0.12, 0.15, 0.20, 0.25, &
      0.04, 0.04, 0.03, 0.04, 0.05, 0.06, 0.07, 0.07, 0.07, 0.08, &
      0.03, 0.03, 0.04, 0.05, 0.05, 0.05, 0.05, 0.05, 0.05, 0.05, &
      0.03, 0.03, 0.03, 0.04, 0.04, 0.04, 0.05, 0.05, 0.04, 0.04, &
      0.02, 0.02, 0.02, 0.04, 0.04, 0.04, 0.04, 0.04, 0.03, 0.03 /

      data o3lo4 /  &
      14.2, 13.8, 13.2, 12.5, 11.7, 10.5, 8.60, 7.80, 7.50, 6.60, &
      12.5, 12.4, 12.2, 11.7, 10.8, 9.80, 7.80, 7.20, 6.50, 6.10, &
      10.6, 10.5, 10.4, 10.1, 9.60, 9.00, 7.10, 6.80, 6.10, 5.90, &
      7.00, 7.40, 7.90, 7.80, 7.60, 7.30, 6.20, 6.10, 5.80, 5.60, &
      4.20, 4.60, 5.10, 5.60, 5.90, 5.90, 5.90, 5.80, 5.60, 5.30, &
      2.10, 2.30, 2.60, 2.90, 3.50, 4.30, 4.80, 4.90, 5.10, 5.10, &
      0.70, 0.80, 1.00, 1.50, 2.00, 2.80, 3.50, 3.60, 3.70, 4.00, &
      0.15, 0.20, 0.40, 0.50, 0.60, 1.40, 2.10, 2.20, 2.30, 2.50, &
      0.08, 0.10, 0.15, 0.25, 0.30, 0.90, 1.20, 1.30, 1.40, 1.60, &
      0.07, 0.08, 0.10, 0.14, 0.20, 0.50, 0.70, 0.90, 0.90, 0.80, &
      0.05, 0.06, 0.08, 0.12, 0.14, 0.20, 0.35, 0.40, 0.60, 0.50, &
      0.05, 0.05, 0.08, 0.09, 0.09, 0.09, 0.11, 0.12, 0.15, 0.18, &
      0.04, 0.05, 0.06, 0.07, 0.07, 0.08, 0.08, 0.08, 0.08, 0.08, &
      0.04, 0.04, 0.05, 0.07, 0.07, 0.07, 0.07, 0.07, 0.06, 0.05, &
      0.02, 0.02, 0.04, 0.05, 0.05, 0.05, 0.05, 0.05, 0.04, 0.04, &
      0.02, 0.02, 0.03, 0.04, 0.04, 0.04, 0.04, 0.04, 0.03, 0.03 /

contains
 
!#######################################################################

      subroutine zonal_ozone_init (season)

!-----------------------------------------------------------------------
!
!       initialization routine for retrieval of zonal ozone amounts.
!
!   input argument
!   --------------
!
!      season     scalar integer between 0-5
!                 where 1-4 uses fixed data (1=winter, 2=spring, etc.)
!                 season=0 is annual mean ozone
!                 season=5 is seasonal varying ozone
!

      integer, intent(in) :: season

!-----------------------------------------------------------------------
      real p(81)
      real ro31(10,41),ro32(10,41),tempn(19),duo3n(19,41),data4(19,81,4)
      integer :: j,k,kk,ncase,n, unit
      character(len=34) :: err_string
!-----------------------------------------------------------------------
!***********************************************************************
!************************ initialization *******************************
!***********************************************************************
      if (iseason /= -1) then
          call error_mesg ('zonal_ozone_init',  &
                       'initialization has already been called.', FATAL)
      endif

      if (season < 0 .or. season > 5) then
          write (err_string,9001) season
 9001     format ('invalid value of season=',i10)
          call error_mesg ('zonal_ozone_init', err_string, FATAL)
      endif
      iseason=season

!---- print version number to logfile ----

   unit = open_file ('logfile.out', action='append')
   if (get_my_pe() == 0) &
   write (unit,'(/,80("="),/(a))') trim(version), trim(tag)
   call close_file (unit)

!------- constants -----------------------------------------------------

         twopi=4.0*acos(0.0)
         rad2deg=360./twopi

!-----------------------------------------------------------------------
!-------standard pressure levels (p) and interfaces (ph)----------------

         ph(:)=ph3(:)*pref
         p (:)=p3 (:)*pref

!-----------------------------------------------------------------------
!------------ load arrays ro31, ro32 as in dicks program ---------------

      do k=1,25
         ro31(:,k)=o3hi(:,k)
         ro32(:,k)=o3hi(:,k)
      enddo

!-----------------------------------------------------------------------
!----------------- setup for no. hemis. seasons ------------------------
!                  ncase=1  ==> n=2  (spring)
!                  ncase=2  ==> n=4  (fall)
!                  ncase=3  ==> n=1  (winter)
!                  ncase=4  ==> n=3  (summer)
!-----------------------------------------------------------------------

                      do 3000 ncase=1,4

!-----------------------------------------------------------------------

      if (ncase == 1) n=2
      if (ncase == 2) n=4
      if (ncase == 3) n=1
      if (ncase == 4) n=3

      if (ncase == 1.or.ncase == 2) then
         do k=26,41
            ro31(:,k)=o3lo1(:,k-25)
            ro32(:,k)=o3lo2(:,k-25)
         enddo
      endif

      if (ncase == 3.or.ncase == 4) then
         do k=26,41
            ro31(:,k)=o3lo3(:,k-25)
            ro32(:,k)=o3lo4(:,k-25)
         enddo
      endif

      do k=1,41
         do j=1,10
            duo3n(j  ,k)=ro31(11-j,k)
            duo3n(j+9,k)=ro32(j   ,k)
         enddo
         duo3n(10 ,k)=0.50*(ro31(1,k)+ro32(1,k))
      enddo

!***for ncase=2 or ncase=4,reverse latitude arrangement of corr. season

      if (ncase == 2.or.ncase == 4) then
         do k=1,41
            do j=1,19
               tempn(j)=duo3n(20-j,k)
            enddo
            do j=1,19
               duo3n(j,k)=tempn(j)
            enddo
         enddo
      endif

!-----------------------------------------------------------------------
!            vertical interp between original data
!            bessels half-point interpolation formula
!-----------------------------------------------------------------------

      do kk=4,78,2
        k=kk/2
        o3data(:,kk,n)=0.50*(duo3n(:,k)+duo3n(:,k+1))  &
               -(duo3n(:,k+2)-duo3n(:,k+1)-duo3n(:,k)+duo3n(:,k-1))/16.
      enddo

        o3data(:, 2,n)=0.50*(duo3n(:,2)+duo3n(:,1))
        o3data(:,80,n)=0.50*(duo3n(:,41)+duo3n(:,40))

! ------ put unchanged data into new array ------

      do kk=1,81,2
        k=(kk+1)/2
        o3data(:,kk,n)=duo3n(:,k)
      enddo

!-----------------------------------------------------------------------

 3000                     continue

!-----------------------------------------------------------------------
!-----------------------------------------------------------------------
!---------------- prepare seasonal interpolation -----------------------
!-----------------------------------------------------------------------

      if (iseason == 5) then
         data4(:,:,1)=0.25*(o3data(:,:,1)+o3data(:,:,2)  &
                           +o3data(:,:,3)+o3data(:,:,4))
         data4(:,:,2)=0.50*(o3data(:,:,2)-o3data(:,:,4))
         data4(:,:,3)=0.50*(o3data(:,:,1)-o3data(:,:,3))
         data4(:,:,4)=0.25*(o3data(:,:,1)-o3data(:,:,1)  &
                           +o3data(:,:,3)-o3data(:,:,4))
         o3data(:,:,1)=data4(:,:,1)
         o3data(:,:,2)=data4(:,:,2)
         o3data(:,:,3)=data4(:,:,3)
         o3data(:,:,4)=data4(:,:,4)
      endif

!-----------------------------------------------------------------------
!---------------- prepare annual mean data -----------------------------
!-----------------------------------------------------------------------

      if (iseason == 0) then
         data4(:,:,1)=0.25*(o3data(:,:,1)+o3data(:,:,2)  &
                           +o3data(:,:,3)+o3data(:,:,4))
         o3data(:,:,1)=data4(:,:,1)
         iseason=1
      endif


!---- set fractional year to force init time interp ----

                 current_fyear = -1.0

!-----------------------------------------------------------------------
      end subroutine zonal_ozone_init

!#######################################################################

      subroutine geto3_3d (time, lat, phalf, ozone)

!-----------------------------------------------------------------------
!
!    input:   time
!             lat       latitude in degrees, dimensioned by ncol
!             phalf     pressure at model layer interfaces,
!                          dimensioned mxcol x nlev+1
!
!    output:  ozone     ozone mixing ratio at model levels,
!                          dimensioned mxcol x nlev
!
!             note:  the first dimension of phalf and ozone is mxcol,
!                    however only the first ncol points are processed.
!

type(time_type), intent(in)  :: time
real,            intent(in)  :: lat(:,:), phalf(:,:,:)
real,            intent(out) :: ozone(:,:,:)

!-----------------------------------------------------------------------
real, parameter :: rlag=1./24.
real, dimension(81) :: profile, dp, dp1, dp2, dp3, dp4, o3col
real    :: rang,rsin1,rcos1,rcos2,phd,th,fyear
integer :: i,j,k,l,j1,j2
!-----------------------------------------------------------------------

      if (iseason == -1) then
         call zonal_ozone_init (5)
      endif

!-----------------------------------------------------------------------
!--------------time interpolation if seasonally varying-----------------
!-----------------------------------------------------------------------
      if (iseason == 5) then

         fyear = fraction_of_year (time)

         if (fyear /= current_fyear) then
             rang=twopi*(fyear-rlag)
             rsin1=sin(rang)
             rcos1=cos(rang)
             rcos2=cos(2.0*rang)

             rstd(:,:)=       o3data(:,:,1) + rsin1*o3data(:,:,2)  &
                      + rcos1*o3data(:,:,3) + rcos2*o3data(:,:,4)

             current_fyear = fyear
         endif

      else

!-----------------------------------------------------------------------
!--------------no interpolation: not seasonally varying-----------------
!-----------------------------------------------------------------------
            rstd(:,:)= o3data(:,:,iseason)

      endif
!-----------------------------------------------------------------------
!-----------------------------------------------------------------------
      do l=1,81
         dp (l)=ph(l+1)-ph(l)
      enddo
!=======================================================================
!---- horizontal loops -----
   do j=1,size(lat,2)
   do i=1,size(lat,1)
!=======================================================================
!----------------horizontal interpolation-------------------------------
      phd=max(min(lat(i,j)*rad2deg,90.),-90.)
      j1=10.000-phd*0.10
      j1=max(min(j1,18),1)
      j2=j1+1
      th=(10-j1)-phd*0.10

      profile(:)=rstd(j1,:)+th*(rstd(j2,:)-rstd(j1,:))

! ======================
! --- vertical loop ----
   do k=1,size(ozone,3)
! ======================

!*****calculate layer-mean ozone mixing ratio for each model level
!*****loop to calculate sums to get layer ozone mean

         do l=1,81
            dp1(l)=ph(l+1)-phalf(i,j,k)
            dp2(l)=phalf(i,j,k+1)-ph(l)
            dp3(l)=ph(l+1)-phalf(i,j,k+1)
            dp4(l)=ph(l)-phalf(i,j,k)
         enddo

         where (dp1(:) < 0.0) dp1(:)=0.0
         where (dp2(:) < 0.0) dp2(:)=0.0

         do l=1,81
            o3col(l)=0.0
            if ( dp3(l) < 0.0 ) then
               if ( dp4(l) < 0.0 ) then
                  o3col(l)=profile(l)*dp1(l)
               else
                  o3col(l)=profile(l)*dp(l)
               endif
            else
               if ( dp4(l) < 0.0 ) then
                  o3col(l)=profile(l)*(phalf(i,j,k+1)-phalf(i,j,k))
               else
                  o3col(l)=profile(l)*dp2(l)
               endif
            endif
         enddo

         ozone(i,j,k)=sum(o3col(:))

         ozone(i,j,k)=ozone(i,j,k)/(phalf(i,j,k+1)-phalf(i,j,k))

!--- code to cover case when surface pressure is greater than pref -----

         if (ph(82) < phalf(i,j,k+1)) ozone(i,j,k)=profile(81)
!
!!!      if (ozone(i,j,k) > 0.0) go to 99
!
!     code to cover case when model resolution is so fine that no value
!     of ph(l) in the ozone data array falls between phalf(i,j,k+1) and
!     phalf(i,j,k).   procedure is to simply grab the nearest value from
!     o3data

!!!      do l=1,81
!!         if (dp4(l) < 0.0 .and. dp3(l) >= 0.0) ozone(i,j,k)=profile(l)
!!!      enddo

!=======================================================================
   enddo
   enddo
   enddo
!=======================================================================

!       convert units from micrograms/gram to kg/kg

             ozone(:,:,:)=ozone(:,:,:)*1.e-6

!-----------------------------------------------------------------------

      end subroutine geto3_3d

!#######################################################################

subroutine geto3_2d (time, lat, phalf, ozone)

!-----------------------------------------------------------------------
type(time_type), intent(in)       :: time
real, intent(in),  dimension(:)   :: lat
real, intent(in),  dimension(:,:) :: phalf
real, intent(out), dimension(:,:) :: ozone
!-----------------------------------------------------------------------
real,dimension(size(lat,1),1)                 :: lat3
real,dimension(size(phalf,1),1,size(phalf,2)) :: phalf3
real,dimension(size(ozone,1),1,size(ozone,2)) :: ozone3
!-----------------------------------------------------------------------

      lat3(:,1)    =lat(:)
      phalf3(:,1,:)=phalf(:,:)

      call geto3_3d (time, lat3, phalf3, ozone3)

      ozone(:,:)=ozone3(:,1,:)

!-----------------------------------------------------------------------

      end subroutine geto3_2d

!#######################################################################

end module zonal_ozone_mod

